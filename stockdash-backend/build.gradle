plugins {
    id 'org.springframework.boot' version '3.5.11'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.3.0'
    implementation 'io.github.resilience4j:resilience4j-micrometer:2.3.0'
    implementation 'org.apache.commons:commons-csv:1.10.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test', Test) {
    useJUnitPlatform()
    exclude '**/*IT.class'
}

tasks.register('integrationTest', Test) {
    description = 'Runs MySQL-backed integration tests.'
    group = 'verification'
    useJUnitPlatform()
    include '**/*IT.class'
    shouldRunAfter tasks.named('test')
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

tasks.named('check') {
    dependsOn tasks.named('integrationTest')
}

tasks.named('bootRun', org.springframework.boot.gradle.tasks.run.BootRun) {
    description = 'Runs the backend for local development (mysql + local seed).'
    args '--spring.profiles.active=mysql,seed-local'
}

tasks.register('bootRunSeed', org.springframework.boot.gradle.tasks.run.BootRun) {
    group = 'application'
    description = 'Runs the backend with demo seed profile enabled.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.temadison.stockdash.backend.StockDashboardApplication'
    args '--spring.profiles.active=seed'
}

tasks.register('bootRunSeedLocal', org.springframework.boot.gradle.tasks.run.BootRun) {
    group = 'application'
    description = 'Runs the backend with local seed profile enabled.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.temadison.stockdash.backend.StockDashboardApplication'
    args '--spring.profiles.active=seed-local'
}

tasks.register('bootRunMysql', org.springframework.boot.gradle.tasks.run.BootRun) {
    group = 'application'
    description = 'Runs the backend with mysql + local seed profiles enabled.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.temadison.stockdash.backend.StockDashboardApplication'
    args '--spring.profiles.active=mysql,seed-local'
}
